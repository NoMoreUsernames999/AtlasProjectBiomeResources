buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:4.1.10'
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.12.2-2.0.0-B'
group = 'expansebiomeresource.mod'
archivesBaseName = 'expansebiomeresource'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    google()
    mavenCentral()
    maven {
        url = 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    //mods
    implementation fg.deobf("curse.maven:just-enough-dimensions-254317:2932048")
    implementation fg.deobf("curse.maven:biomes-o-plenty-220318:2842510")
    implementation fg.deobf("curse.maven:project-vibrant-journeys-300297:2775566")
}

processResources {
    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": "1.12.2"
    }
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"
}

jar {
    manifest {
        attributes([
            "FMLAT": "accesstransformer.cfg",
            "Specification-Title": "expansebiomeresource",
            "Specification-Vendor": "expansebiomeresource",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "1",
            "Implementation-Vendor" :"expansebiomeresource",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
